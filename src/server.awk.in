#! /usr/bin/gawk -f
#
# Copyright 2010 Denis Roio <jaromil@dyne.org>
#
# This source code is free software; you can redistribute it and/or
# modify it under the terms of the GNU Public License as published 
# by the Free Software Foundation; either version 2 of the License,
# or (at your option) any later version.
#
# This source code is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# Please refer to the GNU Public License for more details.
#
# You should have received a copy of the GNU Public License along with
# this source code; if not, write to:
# Free Software Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA

BEGIN {
  IGNORECASE = 1
  quit     = 0                         # script exits if quit > 0
  port     = 8080                      # port number
  host     = "/inet/tcp/" port "/0/0"  # host string
  url      = "http://localhost:" port  # server url
  status   = 200                       # 200 == OK
  reason   = "OK"                      # server response
  RS = ORS = "\r\n"                    # header line terminators
  doc      = "doc/index.html"          # html default document

  print ".:| AutOrg Web Server running on",url,"|:."

  while (!quit) {

      if ($1 == "GET")  doc = substr($2, 2)

      if (doc ~ "cmd$") Exec(doc)  # FIXME: should be a POST (but we're on localhost)
      else              Serve(doc)

      if (quit) break
      close(host)     # close client connection
      print "Closed client connection"
      host |& getline # wait for new client request
      print "New Request:",$0
  }
  # server terminated...
  print "Received Command QUIT: good bye!"
  close(host)
  exit
}

function Exec(cmd) {
    if ( cmd ~ /quit.cmd/) quit = 1
}

function Serve(page) {

    if ( page == "" ) page = "index.html"

    page_file = "%autorg_path%/pub/" page

    # FIXME: echo -n is not portable
    file_exists_p = "test -f " page_file " && echo -n 0 || echo -n 1" 
    file_exists_p | getline file_not_found
    close(file_exists_p)
    if ( file_not_found+1 > 1 ) {
        page      = "doc/404.html"
        page_file = "%autorg_path%/pub/" page
        status    = 404
        reason    = "File Not Found"
    }

    data = ""
    while ((getline line < page_file) > 0)
        data  = data line

    if( page ~ /html$/) content = "text/html"
    else if( page ~ /jpg$/)  content = "image/jpeg"
    else if( page ~ /ico$/)  content = "image/x-icon"
    else if( page ~ /png$/)  content = "image/png"
    else if( page ~ /pdf$/)  content = "application/pdf"
    else content = "text/plain"

    print "HTTP/1.1", status, reason      |& host
    print "Content-Type:", content        |& host
    print "Content-Length:", length(data) |& host
    print "Connection: Close"             |& host
    print ORS ORS data                    |& host
    close(page)
    print status,reason," [ GET",page,"]"
}
